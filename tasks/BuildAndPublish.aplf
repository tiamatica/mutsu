 BuildAndPublish;⎕TRAP;⎕IO;⎕ML;root;version;template;ns;json;packagename;parms;api_key;tatin;git_tag
 ⎕IO←⎕ML←1
 {}⎕EX⊃⎕SI
 ⎕TRAP←0 'E' '⎕←↑⎕DM,⎕XSI,¨''['',¨(⍕¨⎕LC),¨'']'' ⋄ ⎕OFF 1'
 (git_tag api_key)←2 ⎕NQ '.' 'GetEnvironment' ('TAG_VERSION' 'API_KEY')
 :If 0∊≢¨git_tag  api_key
    ⎕←'Git tag or Tatin API key not specified. Aborting'
    ⎕OFF 1
 :EndIf
 :If 0=≢'^v?[0-9]+.[0-9]+.[0-9]+'⎕S'&'⊢git_tag
    ⎕←'Not a valid version number. Aborting'
    ⎕OFF 1
 :EndIf
 version←'v'((=∘⊃)↓⊢)git_tag
 ⎕←'Build version: ',version
 root←⊃1 ⎕NPARTS ''
 ⎕←'Working directory: ',root
 ⎕WSID←root,'Mutsu'  
 ⎕←'Establish Tatin in ⎕SE'
 ⎕←⎕SE.Tatin.Version
 ⎕←'Build package'
 3 ⎕MKDIR './build'
 parms←⎕SE.Tatin.CreateBuildParms root
 parms.targetPath←'./build'
 parms.version←version
 packagename←⎕SE.Tatin.BuildPackage parms

 ⍝ Publish to Tatin test server
 ⎕←'Publish package ',packagename,' to [tatin-test]'
 ⍝⎕←⎕SE.Tatin.PublishPackage packagename '[tatin-test]'

 ⍝ Publish to Tatin server
 ⍝ ⎕←'Publish package ',packagename,' to [tatin]'
 ⍝ tatin←⎕SE.Tatin.MyUserSettings.GetRegistry'[tatin]'
 ⍝ tatin.api_key←api_key
 ⍝ ⎕←⎕SE.Tatin.PublishPackage packagename '[tatin]'
 ⎕OFF 0

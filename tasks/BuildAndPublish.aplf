 BuildAndPublish;⎕TRAP;⎕IO;⎕ML;root;version;packagename;parms;api_key;tatin;git_tag; registry
 ⎕IO←⎕ML←1
 {}⎕EX⊃⎕SI
 ⎕TRAP←0 'E' '⎕←↑⎕DM,⎕XSI,¨''['',¨(⍕¨⎕LC),¨'']'' ⋄ ⎕OFF 1'
 (git_tag registry api_key)←2 ⎕NQ '.' 'GetEnvironment' ('TAG_VERSION' 'TATIN_REGISTRY' 'API_KEY')
 :If 0∊≢¨git_tag registry api_key
    ⎕←'Git tag, registry or API key not specified. Aborting'
    ⎕OFF 1
 :EndIf
 version←⊃'^v?(\d+\.\d+\.\d+(-[^.]+)?)$'⎕S'\1'⊢git_tag
 :If 0=≢version
    ⎕←'Not a valid version number. Aborting'
    ⎕OFF 1
 :EndIf
 
 ⎕←'Build version: ',version
 root←⊃1 ⎕NPARTS ''
 ⎕←'Working directory: ',root
 ⎕WSID←root,'Mutsu'  
 ⎕←'Establish Tatin in ⎕SE'
 ⎕←⎕SE.Tatin.Version
 ⎕←'Build package'
 3 ⎕MKDIR './build'
 parms←⎕SE.Tatin.CreateBuildParms root
 parms.targetPath←'./build'
 parms.version←version
 packagename←⎕SE.Tatin.BuildPackage parms

 ⎕←'Publish package ',packagename,' to ',registry
 tatin←⎕SE.Tatin.MyUserSettings.GetRegistry registry
 tatin.api_key←api_key
 ⎕←⎕SE.Tatin.PublishPackage packagename registry
 ⎕OFF 0

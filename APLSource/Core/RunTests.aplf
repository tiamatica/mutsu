 {res}←RunTests arg;res;testsuite;testnamespaces;testnamespace;cfg;rc;msg;content;activate;upgrade
⍝ Run tests and return result.
⍝ arg is either a configuration space (NewConfiguration)
⍝ or a vector of namespaces to scan for tests.
⍝ If arg is empty, scan calling space for tests.
⍝ Return a result space.

 :If 0∊⍴arg                            ⍝ if empty...
     cfg←NewConfiguration
     cfg.TestSpaces,←2⊃⎕RSI            ⍝ ...scan calling space
 :ElseIf 1<≢arg                        ⍝ if more than one
 :OrIf (⍕arg)≢'[Mutsu configuration]'  ⍝ or not a config space
     cfg←NewConfiguration
     cfg.TestSpaces,←arg               ⍝ ...assume refs to scan
 :Else
     cfg←arg
 :EndIf
 ConfigValidation cfg

 res←NewResultSet
 testnamespaces←⊃,/{
     0∊⍴subspaces←⍵.⎕NL ¯9.1:⍵
     subspaces/⍨←((⊂⍕⍵),¨'.',¨subspaces)≡¨⍕¨⍵.⍎¨subspaces
     0∊⍴subspaces:⍵
     ⍵,⊃,/∇¨⍵.⍎¨subspaces
 }¨cfg.TestSpaces
 activate upgrade←'inactive' 'coverage'≡¨2↑⎕PROFILE'state'
 {}⎕PROFILE⍣upgrade⊢'clear'
 {}⎕PROFILE⍣(activate∨upgrade)⊢'start' 'none'
 :For testnamespace :In testnamespaces
     testsuite←cfg RunTestsuite testnamespace
     :If ~0∊⍴testsuite.Testcases
         res.Testsuites,←testsuite
     :EndIf
 :EndFor
 {}⎕PROFILE⍣activate⊢'clear'
 res.TimeElapsed←86400×-/1 ⎕DT ⎕TS res.Timestamp
 :If ~0∊⍴res.Testsuites
     res.(Assertions Errors Failures Skipped Tests)←+⌿↑res.Testsuites.(Assertions Errors Failures Skipped Tests)
 :EndIf
 :Select cfg.ReportType
 :Case API.REPORTTYPE.SESSION
     ⎕←↑ReportText res
 :Case API.REPORTTYPE.TXT
     content←ReportText res
     content SaveReport cfg
 :Case API.REPORTTYPE.JUNIT
     content←ReportJUnit res
     content SaveReport cfg
 :Case API.REPORTTYPE.GITHUB
     ⎕←'Posting to GitHub'
     rc msg←ReportGitHub res ⍝ Try posting to GitHub
     :If rc=0
         ⎕←msg.HttpStatus msg.Data
     :Else
         ⎕←msg
         ⎕←↑ReportText res
     :EndIf
 :EndSelect
⍝

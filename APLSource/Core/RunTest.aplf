 r←cfg RunTest(testspace testfn);res;⎕TRAP;dmx;∆ST;∆DMX;toString
⍝ Run test testfn using configuration cfg

 r←NewTestcase testfn
 r.(File Line)←(⊂4 5)⌷GetObjectInfo(⍕testspace),'.',testfn

 ∆DMX←0
 ∆ST←0⍴⊂''

 ⎕TRAP←(~cfg.Debug)/(98 'C' '→Fail')((0 1000)'E'((⍕⎕THIS),'.ErrorHandler'))

 testspace.⍎testfn
 :Return

Fail:
 dmx←⎕DMX
 toString←{1↓⊃,/(⎕UCS 10),¨⍵}
 ⎕TRAP←0⍴⎕TRAP
 r.ResultCode←dmx.ENX    ⍝ API.TESTRESULT
 r.Message←dmx.Message
 :If r.ResultCode=API.TESTRESULT.FAIL
     r.Type←dmx.EM
     r.Message←r.Message{~0∊⍴⍺:⍺ ⋄ ⍵}2⊃dmx.DM
     r.Details←toString dmx.DM
 :ElseIf r.ResultCode=API.TESTRESULT.ERROR
     r.Message←∆DMX.Message
     r.Details←toString ∆DMX.DM,∆ST
     r.Type←∆DMX.EM
 :EndIf

⍝
